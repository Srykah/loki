function(loki_find_dep depName depVer depComps)
	find_package(${depName} ${depVer} COMPONENTS "${depComps}" CONFIG)
	set(${depName}_ROOT "${${depName}_ROOT}"
		CACHE PATH "${depName} root directory"
		)
	if(NOT ${depName}_FOUND)
		message(SEND_ERROR "${depName} not found; please set ${depName}_ROOT.")
	else()
		loki_print_variables_with_pattern(${depName})
	endif()
endfunction()

function(loki_find_all_deps depList)
	foreach(dep ${depList})
		string(REPLACE "," ";" depData ${dep})
		list(LENGTH depData depDataLen)
		list(GET depData 0 depName)
		string(STRIP ${depName} depName)
		list(GET depData 1 depVer)
		string(STRIP ${depVer} depVer)
		if(depDataLen GREATER 2)
			list(GET depData 2 depComps)
			string(STRIP ${depComps} depComps)
			string(REPLACE " " ";" depComps ${depComps})
		endif()
		loki_find_dep(${depName} ${depVer} "${depComps}")
	endforeach()
endfunction()
